buildscript {
    ext.agp_version = '8.2.0'
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/emakryo/rust-android-gradle")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath 'io.github.emakryo.rust-android-gradle:plugin:+'
    }
}

plugins {
    id("com.dorongold.task-tree") version "2.1.1"
}

apply plugin: 'com.android.application'
apply plugin: 'io.github.emakryo.rust-android-gradle.rust-android'

android {
    namespace "com.nishtahir.androidrust"
    compileSdkVersion 33
    ndkVersion "23.1.7779620"
    buildFeatures.buildConfig true

    defaultConfig {
        applicationId "com.nishtahir.androidrust"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "FEATURES", "\"foo\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

cargo {
    module = "../rust"
    targets = ["x86_64", "arm64"]
    libname = "rust"
    targetDirectory = "../rust/targets/base"
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.ext:truth:1.5.0"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:orchestrator:1.4.2"
}

android.applicationVariants.all { variant ->
    def name = variant.name.capitalize()
    tasks.named("merge${name}JniLibFolders"){
        dependsOn("cargoBuildAndroid${name}")
    }
}
